class Sphere {

   field int x, y; 
   field int size; 
   field Racket left;
   field Racket right;
   field Table table;

   field int vx, vy;
   field int newX, newY;

   field int startX, startY;

   constructor Sphere new(int _x, int _y, int _size, Racket _left, Racket _right, Table _table) {
      let x = _x;
      let y = _y;
      let startX = _x;
      let startY = _y;
      let size = _size;
      let left = _left;
      let right = _right;
      let table = _table;

      do restoreVector();

      do draw(x, y);

      return this;
   }

   method int getX() { return x; }
   method int getY() { return y; }

   method int getNewX() { return newX; }
   method int getNewY() { return newY; }

   method int getSize() { return size; }
   

   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   method void draw(int sx, int sy) {
      do Screen.setColor(true);
      do Screen.drawCircle(sx, sy, size);
      return;
   }

   method void erase(int sx, int sy) {
      do Screen.setColor(false);
      do Screen.drawCircle(sx, sy, size);
      return;
   }
 
   method void move(int _x, int _y) {
      do erase(x, y);
      let x = _x;
      let y = _y;
      do draw(x,y);
      return;
   }  

    method void updateBallPosition() {
      var int newX, newY;
      var int hitPos;
      var int goalState;
      // Рассчитываем новую позицию
      let newX = x + vx;
      let newY = y + vy;

      // Отскок от верхней границы
      if (newY - size < 8) {
         let vy = -vy;
      }

      // Проверка столкновения с левой ракеткой
      if (((newX - size) < (left.getX() + left.getSize() + 5)) & 
          ((newY + size) > left.getY()) & ((newY - size) < (left.getY() + left.getSize()))) {

         let hitPos = (newY - left.getY()) * 4 / left.getSize(); // Диапазон 0-4
         let vy = (hitPos - 2);                                  // vy от -2 до 2
         let vx = -vx;           // Инвертируем горизонтальную скорость
         
         // Добавляем минимальную скорость для предотвращения "залипания"
         if (vy = 0) { let vy = 1; }
      }

      // перерисовываем ракетку на случай стирания
      if (newX = left.getSize()*3){
            do left.draw();
      }

      // Проверка столкновения с правой ракеткой
      if (((newX + size) > (right.getX() - 5)) & 
          ((newY + size) > right.getY()) & ((newY - size) < (right.getY() + right.getSize()))) {

         let hitPos = (newY - right.getY()) * 4 / right.getSize();
         let vy = (hitPos - 2);
         let vx = -vx;

         if (vy = 0) { let vy = -1; }
      }

      // перерисовываем ракетку на случай стирания
      if (newX = 512 - right.getSize()*3){
            do right.draw();
      }

      // Проверка столкновения со столом
      if (((newX - size) > table.getX()) & ((newX + size) < (table.getX() + table.getWidth())) &
         (newY + size > (table.getY() - 4))) {
         let vy = -vy;
      }

      // Проверяем, забит ли гол правой ракеткой, если да, то меняем начальный вектор движения по x на противоположный (так задумано)
      let goalState = goal(newX, newY, x, y);
      if (goalState = 1){
         let vx = -2;
         return;
      } 

      // Проверяем, забит ли гол левой
      if (goalState = 0) {
         return;
      }

      // Двигаем мяч в новую позицию
      do move(newX, newY);
      return;
   }

   // Проверка условия выхода мяча за левую грань и внизу слева
   method boolean outOfLeft(int newX, int newY, int diff){
      return (((newX - size) < (4 + diff)) | (((newY + size) > (table.getY() - diff)) & ((newX - size) < table.getX())) | ((newX - size) < (left.getX()+left.getSize())/3) | ((newY < size + 4) & ((newX - size) < (4 + diff))));
   }

   // Проверка условия выхода мяча за правую грань и внизу
   method boolean outOfRight(int newX, int newY, int diff){
      return (((newX + size) > right.getX()/3) | ((newX + size) > (508 - diff)) | (((newY + size) > (table.getY() - diff)) & ((newX + size) > (table.getX() + table.getWidth()))) | ((newY < size + 4) & ((newX + size) > (508 - diff))));
   }

   // Рестартим мяч в новой позиции при забитом голе
   method int goal(int newX, int newY, int x, int y){
      if (outOfLeft(newX, newY, 0)){
         do erase(x, y);  
         do restore();
         return 1;
      }
      if (outOfRight(newX, newY, 0)){
         do erase(x, y);  
         do restore();
         return 0;
      }
      return -1;
   }

   // Перерисовка в стартовой позиции
   method void restore() {
      do move(startX, startY);
      do restoreVector();
      return;
   }

   method void restoreVector() {
      let vx = 2;
      let vy = 0;
      return;
   }

}